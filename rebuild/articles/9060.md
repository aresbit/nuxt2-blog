Since some seem to be wondering what HKTs are ...

# Let's talk about Kinds, baby

For the uninitiated, I'll try to do a brief overview of what kinds, and **HKTs (Higher Kinded Types)** are, and a few choices to consider for HKTs in Rust. Hopefully this can shed some light on the topic for some. Understanding this might require some knowledge of [haskell](https://www.haskell.org/), but I'll try to give Rust equivalents where possible.

## [What is a Kind?](https://en.wikipedia.org/wiki/Kind_(type_theory))

Kinds can most succinctly be described as **"types of types"**. In this sense, they are higher-order-types.

*NOTE: To get the kind of something in `ghci`, you can use [`:k `](https://downloads.haskell.org/~ghc/latest/docs/html/users_guide/ghci.html#ghci-cmd-:kind), where `<type>` is the one you want kind information about.*

Let's take a look at the most basic kind: `*`, pronounced: **star** - which is the kind of all **data types**, and can also be called "proper types", or the concrete kind. Just as we have constructors for data types, such as:

```
data Nat = Zero | Succ Nat

-- where

λ> :t Zero
Zero :: Nat
λ> :t Succ
Succ :: Nat -> Nat
λ> :k Nat
Nat :: *
// in Rust - this is a truly inefficient way to implement Nat, but simple to grasp.
enum Nat { Zero, Succ(Box<Nat>) }
```

, we also have [**type constructors**](https://en.wikipedia.org/wiki/Type_constructor) that take types as arguments and produce another type as a result.
The star kind can thus be seen as a type constructor taking zero arguments, i.e: a **nullary** type constructor.

Next, if we add **generics** to the mix, we get other results:

```
data Maybe a = Nothing | Just a

λ> :k Maybe
Maybe :: * -> *
// See: https://doc.rust-lang.org/std/option/enum.Option.html
pub enum Option<T> {
    None,
    Some(T),
}
```

As we see, a [`Maybe`](http://hackage.haskell.org/package/base-4.9.0.0/docs/Prelude.html#t:Maybe), is a **unary** type constructor from a type (of an element in the `Maybe`, in this case) to a specific `Maybe` type, such as `Maybe Nat`, which has kind star, as seen in: `λ> :k Maybe Nat :: *`

If we add another generic parameter, we get a **binary** type constructor. Such an example is the kind of the function type, or the kind of [`Either`](http://hackage.haskell.org/package/base-4.9.0.0/docs/Prelude.html#t:Either) ([`Result`](https://doc.rust-lang.org/std/result/enum.Result.html) in Rust), or a [`Map`](http://hackage.haskell.org/package/containers-0.5.8.1/docs/Data-Map-Lazy.html) (similar to [`BTreeMap`](https://doc.rust-lang.org/std/collections/struct.BTreeMap.html) in Rust):

```
λ> :k (->)
(->) :: * -> * -> *

data Either a b = Left a | Right b
λ> :k Either
Either :: * -> * -> *

λ> :k Map
Map :: * -> * -> *
```

*An aside note:* to actually create a `Map`, the key type must satisfy the [`Ord` trait](https://doc.rust-lang.org/std/cmp/trait.Ord.html) or [type class](http://hackage.haskell.org/package/base-4.9.0.0/docs/Prelude.html#t:Ord).

In GHC, there are other kinds which are of no interest, but [`GHC.Prim.Constraint`](https://downloads.haskell.org/~ghc/7.8.4/docs/html/users_guide/constraint-kind.html) is of special significance.
This is the kind that you get when you fulfill a **type class**, which correspond to **traits** in Rust.
For example, for the [`Eq` type class](http://hackage.haskell.org/package/base-4.9.0.0/docs/Prelude.html#t:Eq) ([Rust equivalent to `Eq`](https://doc.rust-lang.org/std/cmp/trait.Eq.html)):

```
λ> :i Eq
class Eq a where
    (==) :: a -> a -> Bool
    (/=) :: a -> a -> Bool

λ> :k Eq
Eq :: * -> GHC.Prim.Constraint
```

In this sense, in type theory, both type classes and traits are in fact types.

## Higher Kinded Types

So far, everything I've covered is possible in Rust.
But wait, there is more - enter **HKTs (Higher Kinded Types)**.

### Higher Order Functions

Just as there are functions taking functions as arguments (or returning functions), which are called **[HoFs (Higher Order Functions)](https://en.wikipedia.org/wiki/Higher-order_function)**.

Some well known HoFs are: `map`, `fold`, `filter`, `(.)`.

```
twice :: (a -> a) -> (a -> a)
twice f = f . f
// in Rust:
fn twice<A, F>(function: F) -> Box<Fn(A) -> A> 
    where F: 'static + Fn(A) -> A {
    Box::new(move |a| function(function(a)))
}
```

### Higher Order Type-Constructors

Just as there are HoFs of regular types, there are HoFs of kinds, for example: `(k1 -> k2) -> k3`

Examples of type classes with that form are:

- [Functors](http://hackage.haskell.org/package/base-4.9.0.0/docs/Prelude.html#t:Functor):

```
class Functor f where
    fmap :: (a -> b) -> f a -> f b

-- fully expanded:
class Functor (f :: * -> *) where
    fmap :: (a -> b) -> f a -> f b

λ> :k Functor
Functor :: (* -> *) -> GHC.Prim.Constraint

-- An example instance of Functor for Maybe:
instance Functor Maybe where
    fmap f Nothing  = Nothing
    fmap f (Just a) = Just (f a)
```

- [Applicatives](http://hackage.haskell.org/package/base-4.9.0.0/docs/Prelude.html#t:Applicative):

```
class Functor f => Applicative f where
    pure :: a -> f a
    (<*>) :: f (a -> b) -> f a -> f b

λ> :k Applicative
Applicative :: (* -> *) -> GHC.Prim.Constraint

-- An example instance of Applicative for Maybe:
instance Applicative Maybe where
    pure x = Just x
    Nothing  <*> m = Nothing
    (Just f) <*> m = fmap f m
```

- [Monads](http://hackage.haskell.org/package/base-4.9.0.0/docs/Prelude.html#t:Monad):

```
class Applicative m => Monad m where
    (>>=) :: m a -> (a -> m b) -> m b

λ> :k Monad
Monad :: (* -> *) -> GHC.Prim.Constraint

-- An example instance of Monad for Maybe:
instance  Monad Maybe  where
    (Just x) >>= k = k x
    Nothing  >>= _ = Nothing
```

These type classes allow us to reason about data structures as such (assuming they are instances of the type class). Here, `f` and `m` are as expected generic data structures, or unary type constructors of kind `* -> *`, allowing us to be generic over `f t`, where `t` is a type, for example: `f a`, `f b`.

More about monads: http://dev.stephendiehl.com/hask/#monads

### [FlexibleInstances](http://dev.stephendiehl.com/hask/#flexibleinstances)

### [FlexibleContexts](http://dev.stephendiehl.com/hask/#flexiblecontexts)

### [TypeSynonymInstances](http://dev.stephendiehl.com/hask/#typesynonyminstances)

### [MultiParamTypeClasses](http://dev.stephendiehl.com/hask/#multiparam-typeclasses)

Consider the case where we want a polymorphic way to convert between data structures, but preserve the elements. How do we encode this?

```
-- To enable the language feature.
{-# LANGUAGE MultiParamTypeClasses #-}

-- Needed for the Either () instance below:
{-# LANGUAGE FlexibleInstances #-}

-- Needed to be explicit about f, g :: * -> *
{-# LANGUAGE KindSignatures #-}

import Data.Monoid ((<>)) -- for <> operator.

-- Converting between data structures while preserving elements.
class StructureConversion (f :: * -> *) (g :: * -> *) where
    convert :: f a -> g a

-- Maybe and Either () are isomorphic.
instance StructureConversion Maybe (Either ()) where
    convert Nothing  = Left ()
    convert (Just a) = Right a

-- A binary tree:
data BTree a = L | N a (BTree a) (BTree a)
    deriving Show

-- Converting between BTree:s and Lists.
-- NOTE: this is not structure preserving.
-- i.e: converting to list and then back could never guarantee the exact form back.
instance StructureConversion BTree [] where
    convert L         = []
    convert (N e l r) = e : convert l <> convert r
```

Using the definitions:

```
λ> convert (Just 42) :: Either () Int
Right 42

λ> let tree = N 1 (N 2 L L) (N 3 (N 4 L L) L) :: BTree Int
λ> convert tree :: [Int]
[1,2,3,4]
```

### [FunctionalDependencies](http://dev.stephendiehl.com/hask/#multiparam-typeclasses)

When dealing with type classes with multiple parameters, a functional dependency between the parameters `a -> b`, means that `a` uniquely determines `b` - which means that `a1 ≡ a2 -> b1 ≡ b2`

An example of such a type class is [MonadState](http://hackage.haskell.org/package/mtl-2.2.1/docs/Control-Monad-State-Class.html#t:MonadState):

```
class Monad m => MonadState s m | m -> s where
    get   :: m s
    put   :: s -> m ()
    state :: (s -> (a, s)) -> m a
```

Which has kind:

```
λ> :k MonadState
MonadState :: * -> (* -> *) -> GHC.Prim.Constraint
```

### Not only type classes involve HKTs

An example where data types involve HKTs is [StateT](http://hackage.haskell.org/package/mtl-2.2.1/docs/Control-Monad-State-Lazy.html#v:StateT) (the state transformer monad), which looks like:

```
newtype StateT s m a = StateT { runStateT :: s -> m (a, s) }

instance Monad m => MonadState s (StateT s m) where
    get     = state  $ \s -> (s, s)
    put   s = state  $ const ((), s)
    state f = StateT $ return . f

instance Monad m => Monad        (StateT s m) where
    return           = state . (,)
    (StateT f) >>= g = StateT $ f >=> \(a, s1) -> runStateT (g a) s1

instance Monad m => Functor      (StateT s m) where
    fmap = liftM

instance Monad m => Applicative  (StateT s m) where
    pure  = return
    (<*>) = ap
```

And has the kind:

```
λ> :k StateT
StateT :: * -> (* -> *) -> * -> *
```

### More complex HKTs

Here are some examples of more complex HKTs.

- [MonadTrans](http://hackage.haskell.org/package/transformers-0.5.2.0/docs/Control-Monad-Trans-Class.html#t:MonadTrans), kind: `((* -> *) -> * -> *) -> GHC.Prim.Constraint`

```
class MonadTrans t where
    -- | Lift a computation from the argument monad to the constructed monad.
    lift :: (Monad m) => m a -> t m a

instance MonadTrans (StateT s) where
    lift m = StateT $ \si -> (, si) <$> m
```

- [Category](http://hackage.haskell.org/package/base-4.9.0.0/docs/Control-Category.html#t:Category), kind: `(* -> * -> *) -> GHC.Prim.Constraint`

```
class Category cat where
    id :: cat a a
    (.) :: cat b c -> cat a b -> cat a c
```

- [Bob Atkey's indexed monad](http://stackoverflow.com/a/28708799/1063961), kind: `(* -> * -> * -> *) -> GHC.Prim.Constraint`, [an implementation](https://gist.github.com/Centril/a87d72dc753e0cf71133568de53eb935)

```
class IMonad m where
    ireturn :: a -> m i i a
    (>>>=) :: m i j a -> (a -> m j k b) -> m i k b
```

## A note on syntax

In my opinion, one of the really nice things about type classes in haskell is that they are incredibly terse - as opposed to verbose. While the same terseness is not possible in Rust, it should still be an aim to not let things be overly verbose.

## What should be available in Rust?

I believe it is a good idea - as others have put it - to find examples where HKTs are useful in Rust in a mostly zero-cost way. But I can't say I have anything special to contribute here. It might also be good to classify these into the various kinds they have, so that we know the needs of the type system... But it might also be a good idea to start from the kinds and then see if anything useful can be made that have those kinds.

- Should HKTs be possible in `structs` - which are not `traits`, as in the `StateT` example?
- Should `MultiParamTypeClasses` (MPTC) and `FunctionalDependencies` be possible?

MPTCs means that we can't really talk about a `Self` anymore, since they are rather relations between two or more type constructors. Having MPTCs would therefore probably mean that something different than `trait` is needed. Here we see that `traits` and `type classes` diverge in semantics as traits give you trait objects. The keyword `trait` could possibly be reused, but in this case, I find it difficult to see how it would be possible to omit the type constructors `f, g` in `class StructureConversion f g` - especially not in the case of `FunctionalDependencies`.



